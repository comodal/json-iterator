apply plugin: 'java-library'
apply plugin: 'org.javamodularity.moduleplugin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'me.champeau.gradle.jmh'

scmVersion {
  tag.prefix = ''
  repository.pushTagsOnly = true
}

sourceCompatibility = JavaVersion.VERSION_12

project.version = scmVersion.version
project.group = 'systems.comodal'

ext {
  javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
  desc = 'Json Parser'
  bintrayOrg = 'comodal'
  bintrayRepo = 'libraries'
  vcsUrl = 'https://github.com/comodal/json-iterator'
  moduleName = 'systems.comodal.json_iterator'
}

repositories {
  mavenCentral()
}

dependencies {
  components.all(DependenciesAlignmentRule)

  testImplementation 'org.junit.jupiter:junit-jupiter-api:+'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:+'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:+'

  jmh 'org.openjdk.jmh:jmh-core:+'
  jmh 'org.openjdk.jmh:jmh-generator-annprocess:+'
}

jmh {
  include = ['BenchStyles']
  jmhVersion = '+'
  jvmArgs = ['--enable-preview']
  fork = 1
  resultFormat = 'JSON'
  includeTests = false
}

jmhCompileGeneratedClasses {
  options.compilerArgs = ['--enable-preview']
  options.fork = true
  options.debug = true
  options.forkOptions.executable = javaCompilerExecutable
  options.warnings = false
}

compileJmhJava {
  options.compilerArgs = ['--enable-preview']
  options.fork = true
  options.debug = true
  options.forkOptions.executable = javaCompilerExecutable
  options.warnings = false
}

compileJava {
  options.compilerArgs = ['--enable-preview']
}

compileTestJava {
  options.compilerArgs = ['--enable-preview']
}

test {
  moduleOptions {
    runOnClasspath = true
  }
  jvmArgs = ['--enable-preview']
  useJUnitPlatform()
  maxParallelForks = 4
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
    exceptionFormat "full"
    showStandardStreams true
  }
}


final class DependenciesAlignmentRule implements ComponentMetadataRule {
  void execute(ComponentMetadataContext ctx) {
    ctx.details.with {
      if (id.group.startsWith("org.junit.jupiter")) {
        belongsTo("org.junit.jupiter:junit-platform:${id.version}")
      } else if (id.group.startsWith("org.openjdk.jmh")) {
        belongsTo("org.openjdk.jmh:jmh-platform:${id.version}")
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      groupId project.group
      artifactId project.name
      version = project.version
    }
  }
}

bintray {
  user = project.hasProperty("bintrayUser") ? bintrayUser : System.getenv('BINTRAY_USER')
  key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
  publications = ['mavenJava']
  pkg {
    userOrg = project.bintrayOrg
    repo = project.bintrayRepo
    name = project.name
    desc = project.desc
    vcsUrl = project.vcsUrl
    websiteUrl = project.vcsUrl
    issueTrackerUrl = project.vcsUrl + '/issues'
    licenses = ["MIT License"]
    publish = true
    version {
      name = project.version
      vcsTag = project.name + '-' + project.version
      gpg {
        sign = true
        passphrase = project.hasProperty("bintrayGPGPassphrase") ? bintrayGPGPassphrase : System.getenv('BINTRAY_GPG_PASSPHRASE')
      }
    }
  }
}