plugins {
  id 'java-library'
  id 'maven-publish'
}

project.group = 'software.sava'
project.version = "$VERSION"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 21)
  }
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.12.+'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
  useJUnitPlatform()
  testLogging {
    events "skipped", "failed", "standardOut", "standardError"
    exceptionFormat = "full"
    showStandardStreams = true
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  options.addBooleanOption('html5', true)
  options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      groupId = project.group
      artifactId = project.name
      version = project.version

      pom {
        name = project.name
        description = "$DESCRIPTION"
        url = "$VCS_URL"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://github.com/sava-software/json-iterator/blob/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'jpe7s'
            organization = 'Sava Software'
            organizationUrl = 'https://github.com/sava-software'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:sava-software/json-iterator.git'
          developerConnection = 'scm:git:ssh@github.com:sava-software/json-iterator.git'
          url = "$VCS_URL"
        }
      }
    }
  }

  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/sava-software/json-iterator"
      credentials {
        username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
        password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
      }
    }
  }
}
